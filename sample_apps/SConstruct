# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0


import os.path
ajtcl_root_default = '../../../core/ajtcl'
ajtcl_root = ARGUMENTS.get('AJ_TCL_ROOT', ajtcl_root_default)
if not os.path.isfile(ajtcl_root + '/SConscript'):
    print "SConscript in 'AJ_TCL_ROOT=" + ajtcl_root + "' not found; aborting.\n"
    exit(1)

env = SConscript(ajtcl_root + '/SConscript')
if env['VARIANT'] == 'debug':
    env.Append(CPPDEFINES = ['ER_DEBUG_AJSVCAPP=1'])
    env.Append(CPPDEFINES = ['ER_DEBUG_AJSVCALL=1'])
    env.Append(CPPDEFINES = ['AJ_DEBUG_RESTRICT=AJ_DEBUG_INFO'])

env['CPPDEFINES'].remove('AJ_NUM_REPLY_CONTEXTS=8')
env.Append(CPPDEFINES = ['AJ_NUM_REPLY_CONTEXTS=4', 'AJ_MAX_OBJECT_LISTS=9', 'ANNOUNCE_BASED_DISCOVERY=1'])

env.Append(CPPPATH = [Dir('AppsCommon/inc')])
env.Append(CPPPATH = [Dir('AppsCommon/target/' + env['TARG'])])
env.Append(CPPPATH = [Dir('../config/inc')])
env.Append(CPPPATH = [Dir('../notification/inc')])
env.Append(CPPPATH = [Dir('../controlpanel/inc')])
env.Append(CPPPATH = [Dir('../time/inc')])
env.Append(CPPPATH = [Dir('.')])

samples = ['ConfigSample', 'NotificationProducerSample', 'NotificationConsumerSample', 'ControlleeSample', 'TimeServerSample','TimeClientSample']

for sample in samples:
    print ''
    print '                     handling sample:' + sample
    print '=================================================================='
    env['variant_dirs'] = sample
    samp_env = env.Clone()
    env.SConscript('SConscript', variant_dir = 'obj/' + samp_env['variant_dirs'], duplicate = 1, exports = 'samp_env')